    {
        # Count number of other measures found at each point 
        SELECT ?numMeasures (COUNT(?obs2) AS ?count) 
        WHERE {
            {
                # Find the DSDs and check how many measures they have
                SELECT ?dsd (COUNT(?m) AS ?numMeasures) 
                WHERE {
                    ?dsd qb:component/qb:componentProperty ?m.
                    ?m a qb:MeasureProperty .
                } GROUP BY ?dsd
            }
        
            # Observation in measureType cube
            ?subject qb:dataSet/qb:structure ?dsd;
                qb:dataSet ?dataset ;
                qb:measureType ?m1 .
    
            # Other observation at same dimension value
            ?obs2 qb:dataSet ?dataset ;
                qb:measureType ?m2 .
            FILTER NOT EXISTS { 
                ?dsd qb:component/qb:componentProperty ?dim .
                FILTER ( ?dim != qb:measureType )
                ?dim a qb:DimensionProperty .
                ?subject ?dim ?v1 . 
                ?obs2 ?dim ?v2. 
                FILTER (?v1 != ?v2) .
            }
          
        } GROUP BY ?subject ?numMeasures
          HAVING (?count != ?numMeasures)
	}
	
	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-DATA-MODEL-CONSISTENCY-10 | ",
		"All measures present in measures dimension cube: ",
		"In a qb:DataSet which uses a Measure dimension then if there is a Observation for some combination of non-measure dimensions then there must be other Observations with the same non-measure dimension values for each of the declared measures." )
		AS ?violationMessage ) .  
		
	# violation source
	# ----------------
	BIND ( CONCAT( "DATA-CUBE-C-DATA-MODEL-CONSISTENCY-10" )AS ?violationSource ) .
	
	# severity level
	# --------------
	BIND ( ( 'ERROR' ) AS ?severityLevel ) .